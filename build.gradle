def isDirty = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'status', '--porcelain'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    def gitVersionName = stdout.toString().trim()
    return isDirty() ? gitVersionName + '-SNAPSHOT' : gitVersionName
}
version = getVersionName()

repositories {
    mavenCentral()
}

ext {
    sonatypeUser = System.env.SONATYPE_USER
    sonatypePass = System.env.SONATYPE_PASS
}

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'groovy'

group 'org.scoverage'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

configurations {
    scoverage
    compile.extendsFrom scoverage
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    scoverage 'org.scoverage:scalac-scoverage-plugin_2.11:1.0.2'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from "$buildDir/docs/groovydoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

test {
    dependsOn jar
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

if (project.properties.containsKey('signing.keyId')) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.properties.containsKey('signing.keyId')) {
              beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            pom.project {
                name 'GradleScoverage'
                description 'gradle-scoverage is a Gradle plugin for calculating code coverage using Scoverage'
                url 'http://scoverage.org'

                scm {
                    url 'scm:git:https://github.com/scoverage/gradle-scoverage.git'
                    developerConnection 'scm:git:https://github.com/scoverage/gradle-scoverage.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'maiflai'
                    }
                    developer {
                        id 'ubourdon'
                    }
                    developer {
                        id 'D-Roch'
                    }
                }
            }
        }
    }
}
